-- Tinco Andringa (s0159786) & Daniel Moscoviter (s0140155)

  VAR
    checkSignalSanity1 : CheckSignalSanity(S1_red, S1_green);
    checkSignalSanity2 : CheckSignalSanity(S2_red, S2_green);
    checkSignalSanity3 : CheckSignalSanity(S3_red, S3_green);
    checkSignalSanity4 : CheckSignalSanity(S4_red, S4_green);

    check_S1 : CheckSignal(S1_red, FALSE);
    check_S2 : CheckSignal(S2_red, T2_occupied|T3_occupied);
    check_S3 : CheckSignal(S3_red, T2_occupied|T1_occupied);
    check_S4 : CheckSignal(S4_red, FALSE);
    checkBarriers : CheckBarriers(B1_closed, B2_closed, B1_open, B2_open, T1_occupied, T2_occupied, T3_occupied);
    checkTrains : CheckTrains(T1_occupied, T2_occupied, T3_occupied);
    checkWarningDevices : CheckWarningDevices(T1_occupied, T2_occupied, T3_occupied, W1_active, W2_active);
    checkCarsCrossing : CheckCarsCrossing(cars_crossing, T2_occupied);

MODULE CheckSignalSanity(red,green)
LTLSPEC -- Both lights should not be on at the same time
  G (red = !green)

-- Check properties of a signal.
-- red    : signal is red.
-- unsafe : if TRUE then it is unsafe to pass this signal.
MODULE CheckSignal(red,unsafe)
LTLSPEC -- Light should be red in unsafe situation
        -- Safety property
  G (unsafe -> red)

MODULE CheckCarsCrossing(cars, train)
LTLSPEC -- Never should cars be crossing when there is a train on the crossing
        -- Safety property
  G !(train & cars)

CTLSPEC -- It should always be possible that cars eventually cross
        -- Liveness property
  EF cars

MODULE CheckBarriers(b1_closed, b2_closed, b1_open, b2_open, track_1, track_2, track_3)

LTLSPEC -- If theres a train in the middle of the crossing
        -- the barriers should be lowered
        -- Safety property
  G (track_2 -> !b1_open & !b2_open)

LTLSPEC
  G (track_2 -> b1_closed & b2_closed)

LTLSPEC -- The barriers should eventually open
        -- Liveness property
  G (!b1_open -> F b1_open)
LTLSPEC
  G (!b2_open -> F b2_open)

LTLSPEC
  G (b1_closed -> F !b1_closed)

LTLSPEC
  G (b2_closed -> F !b2_closed)

MODULE CheckWarningDevices(track_1, track_2, track_3, w1, w2)
LTLSPEC -- Warnings should sound when a train is coming
        -- Safety property
  G (X track_2 -> w1 & w2)

LTLSPEC -- Warnings should eventually stop
        -- Liveness property
  G (w1 -> F !w1)
LTLSPEC
  G (w2 -> F !w2)

-- LTLSPEC -- Warnings should stop when train has passed and no train is coming
-- Liveness property
--   G ((Y track_2) & !track_2 & X track_2 -> !w1 & !w2)
--
-- LTLSPEC -- warnings should stop when there are no trains
--   G (!X track_1 & !X track_2 & !X track_3 -> !w1 & !w2)

MODULE CheckTrains(track_1, track_2, track_3)
LTLSPEC -- Trains make progress
        -- Liveness property
  G (track_1 -> F !track_1)

LTLSPEC -- Trains make progress
  G (track_2 -> F !track_2)

LTLSPEC -- Trains make progress
  G (track_3 -> F !track_3)

CTLSPEC -- Regardless of the history, the environment can evolve into a situation where no
        -- trains are visible.
        -- Liveness property
  EF (!track_1 & !track_2 & !track_3)

CTLSPEC -- Regardless of the history, the environment can evolve into a situation where a
        -- train crosses the road.
        -- Liveness property
  EF (track_2)
